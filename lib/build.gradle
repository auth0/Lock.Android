plugins {
    id "com.auth0.gradle.oss-library.android" version "0.15.1"
}

logger.lifecycle("Using version ${version} for ${name}")

oss {
    name 'Lock.Android'
    repository 'Lock.Android'
    organization 'auth0'
    description 'The easiest way of securing your Android mobile apps with Auth0 & Lock'

    developers {
        auth0 {
            displayName = 'Auth0'
            email = 'oss@auth0.com'
        }
        lbalmaceda {
            displayName = 'Luciano Balmaceda'
            email = 'luciano.balmaceda@auth0.com'
        }
    }
}

android {
    compileSdkVersion 30

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName project.version
        consumerProguardFiles '../proguard/proguard-gson.pro', '../proguard/proguard-otto.pro', '../proguard/proguard-lock-2.pro'
        // Required since Android Gradle Plugin 4.1 - https://issuetracker.google.com/issues/154275579
        buildConfigField "String", "VERSION_NAME", "\"$versionName\""
    }

    lintOptions {
        disable 'ContentDescription', 'SyntheticAccessor'
        abortOnError true
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            // https://github.com/robolectric/robolectric/issues/5115
            all {
                systemProperty("javax.net.ssl.trustStoreType", "JKS")
                maxHeapSize = "1024m"
            }
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'com.squareup.okhttp:okhttp:2.7.5'
    implementation 'com.squareup:otto:1.3.8'
    api 'com.auth0.android:auth0:1.29.0'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.hamcrest:hamcrest-library:1.3'
    testImplementation 'org.robolectric:robolectric:4.4'
    testImplementation 'org.mockito:mockito-core:3.6.28'
    testImplementation 'com.squareup.okhttp3:mockwebserver:3.9.1'
    testImplementation 'com.jayway.awaitility:awaitility:1.7.0'
    testImplementation 'androidx.test.espresso:espresso-intents:3.3.0'
}